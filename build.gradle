import org.ajoberstar.grgit.*

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.ajoberstar:gradle-git:1.3.2'
    }
}

apply plugin: 'org.asciidoctor.convert'

version '1.0.1'

task('publishDocs', dependsOn: asciidoctor, group: 'release', description: 'Adds the new version of the docs to github pages') << {
    def workingDir = project.file("${project.buildDir}/gh-pages")

    workingDir.deleteDir()

    def repo = Grgit.clone(
            uri: 'git@github.com:adrianjgeorge/docs-sandbox.git',
            refToCheckout: 'gh-pages',
            dir: workingDir,
    )

    //add to versions list
    def versionsFile = project.file("${project.buildDir}/gh-pages/_includes/versions.html")
    def versions = versionsFile.readLines()
    versions.add(0, """<li><a href="./versions/$version/index.html">v$version</a> - 2016-01-11</li>""")
    def writer = versionsFile.newWriter()
    versions.each {
        writer.write(it)
    }
    writer.close()

    copy {
        from "${asciidoctor.outputDir}/html5"
        into "${project.buildDir}/gh-pages/versions/$version"
    }

    repo.with {
        add(patterns: ['.'])
        commit(message: "Adding documentation for $version")
        push()
    }
}
